action (CreateOrder) {
  description (Create order)
  type (BeginTransaction)
  collect {
    input (spaceResort) {
      type (SpaceResort)
      min (Required)
      hidden
      default-init {
        intent {
          goal: FindSpaceResorts
        }
      }
    }
    input(numberOfAstronauts) {
      type(NumberOfAstronauts)
      hidden
      min (Required)
      default-init {
       intent {
         goal-set {
           @prompt-behavior(AlwaysSelection)
           NumberOfAstronauts {
             NumberOfAstronauts(1) NumberOfAstronauts(2) NumberOfAstronauts(3) NumberOfAstronauts(4)
           }
         }
       }
      }
    }
    input (dateTimeExpression) {
      type (time.DateTimeExpression)
      hidden
      min (Required)
      default-init {
        // Prompt
        intent {
          goal { @prompt-behavior(AlwaysElicitation) time.DateInterval }
        }
      }
    }

    input (self) {
      type (self.SelfInfo)
      default-init {
        intent {
          goal: self.GetSelfInfo
          value-set: self.RequiredField {
            self.RequiredField(firstName)
            self.RequiredField(lastName)
            self.RequiredField(emailAddress)
            self.RequiredField(phoneNumber)
          }
        }
      }
    }
    computed-input (item) {
      type (Item)
      min (Required) max(One)
      compute {
        intent {
          goal: FindItems
          value: $expr (spaceResort)
          value: $expr (numberOfAstronauts)
          value: $expr (dateTimeExpression)
        }
      }
    }
  }
  output (Order)
}
