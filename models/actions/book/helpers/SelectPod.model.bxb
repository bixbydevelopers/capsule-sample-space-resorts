action (SelectPod) {
  description (Select a HabitatProd from a list by name)
  type (Search)
  collect {
    input (order) {
      type (Order)
      min (Optional)
      hidden
    }

    input (pods) {
      type (HabitatPod)
      min (Required)
      max (Many)
      default-init {
        if (exists(order)) {
          intent {
            goal: FindPods
            value: $expr(order.item.spaceResort)
            value: $expr(order.item.numberOfAstronauts)
            value: $expr(order.item.dateInterval)
          }
        }
      }
    }

    input(podName) {
      type (PodName)
      min (Required)
    }
  }
  output (HabitatPod) {
    throws {
      error (MultipleMatches) {
        property (matches) {
          type (HabitatPod)
          min (Required)
          max (Many)
        }
        on-catch {
          replan {
            intent {
              goal { HabitatPod @prompt-behavior(AlwaysSelection) }
              value { $expr(matches) }
            }
          }
        }
      }
    }
  }
}
